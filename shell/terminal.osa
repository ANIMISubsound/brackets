#!/usr/bin/osascript
on run argv

    set dir to quoted form of (first item of argv)
    set new_cmd to 0

    set doesExist to false
    try
        tell application "Finder" to get application file id "com.googlecode.iterm2"
        set doesExist to true
    on error
        set doesExist to false
    end try

    if doesExist then
        tell application "System Events"
            if "iTerm" is not in name of processes then
                launch application "iTerm"

                tell process "iTerm"
                    set frontmost to true
                end tell

                set new_cmd to 1
            end if
        end tell

        tell application "iTerm"
            if (new_cmd) is 1 then
                -- iTerm was just launched
                tell the current terminal
                    set cur_session_name to get name of current session
                    if cur_session_name is not "Brackets-Git (bash)" then
                        launch session "Brackets-Git"
                        set name of current session to "Brackets-Git"
                        tell current session
                            write text "cd " & dir & "; git status"
                        end tell
                    end if
                end tell
            else
                -- iTerm was already open
                activate
                tell the current terminal
                    select session "Brackets-Git (bash)"
                end tell
            end if
        end tell
        do shell script "echo ok"

    else

        tell application "System Events"
            if "Terminal" is not in name of processes then
                launch application "Terminal"

                tell application "Terminal"
                    activate
                    do script ""
                end tell

                set new_cmd to 1
            end if
        end tell

        tell application "Terminal"
            if (new_cmd) is 1 then
                -- Terminal was just launched
                do script "cd " & dir & "; git status" in window 1
            else
                -- Terminal was already open
                activate
                do script "cd " & dir & "; git status"
            end if
        end tell

    end if

    do shell script "echo ok"

end run
